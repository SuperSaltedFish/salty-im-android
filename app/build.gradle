apply plugin: 'com.android.application'
apply plugin: 'com.google.protobuf'

android {
    compileSdkVersion buildVersions.compileSdkVersion
    buildToolsVersion buildVersions.buildToolsVersion
    defaultConfig {
        minSdkVersion buildVersions.minSdkVersion
        targetSdkVersion buildVersions.targetSdkVersion
        versionCode buildVersions.versionCode
        versionName buildVersions.versionName
        flavorDimensions buildVersions.flavorDimensions
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        ndk {
            abiFilters 'x86', 'armeabi-v8a'
        }
    }


    signingConfigs {
        config {
            keyAlias 'zhixingye'
            keyPassword 'yezhixing'
            storeFile file('../keystore.jks')
            storePassword 'yezhixing'
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            zipAlignEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
            buildConfigField 'boolean', 'isDebug', 'true'  //是否是debug模式，true的情况下会打印日志，否则不打印

        }
        release {
            minifyEnabled true
            zipAlignEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
            buildConfigField 'boolean', 'isDebug', 'false' //是否是debug模式，true的情况下会打印日志，否则不打印
        }
    }


    lintOptions {
        disable 'GoogleAppIndexingWarning'
    }

    productFlavors {
        produce {
            applicationId "me.zhixingye.salty"
            resValue "string", "app_name", "闲聊"
            buildConfigField 'String', 'ServerUrl', '"https://swallet-equicom-uat.wepayez.com/"'
        }

        dev {
            applicationId "me.zhixingye.salty.dev"
            resValue "string", "app_name", "(DEV)闲聊"
            buildConfigField 'String', 'ServerUrl', '"http://192.168.1.19:9091/"'
        }
    }

    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }

    android.applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "SaltyIM_${variant.buildType.name}_${variant.versionName}" + ".apk"
        }
    }

}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.11.1'
    }
    plugins {
        javalite {
            artifact = 'com.google.protobuf:protoc-gen-javalite:3.0.0'
        }
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:$depVersions.grpc"
        }
    }

    generateProtoTasks {
        all().each { task ->
            task.builtins {

            }
            task.plugins {
                javalite {}
                grpc {
                    option 'lite'
                }
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'

    implementation "io.grpc:grpc-android:$depVersions.grpc"
    implementation "io.grpc:grpc-okhttp:$depVersions.grpc"
    implementation "io.grpc:grpc-protobuf-lite:$depVersions.grpc"
    implementation "io.grpc:grpc-stub:$depVersions.grpc"
    implementation 'javax.annotation:javax.annotation-api:1.3.2'
}
